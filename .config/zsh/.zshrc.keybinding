# --------
#  key binding
# ---------

bindkey -d               # いったんキーバインドをリセット
# bindkey -e             # キーバインドをemacsモードに設定
bindkey -v               # キーバインドをviモードに設定

# Emaccsの便利なショートカットで上書き
# see http://zsh.sourceforge.net/Doc/Release/Zsh-Line-Editor.html#Standard-Widgets
bindkey '^A' beginning-of-line
bindkey '^E' end-of-line
bindkey "^B" backward-char
bindkey "^F" forward-char
bindkey '^N' history-beginning-search-forward-end
bindkey '^P' history-beginning-search-backward-end
bindkey '^U' kill-whole-line
bindkey '^K' kill-line
bindkey '^R' history-incremental-search-backward
bindkey '^S' history-incremental-search-forward
bindkey "^[[Z" reverse-menu-complete  # Shift-Tabで補完候補を逆順する("\e[Z"でも動作する)

bindkey '^v' edit-command-line # Emacs/Vimバインドとかぶらないようにする(see .zshenv)

# MEMO: sdfg shortcuts like hjkl cmd in vim

# Fuzzy cd
z_fd() {
  fd;
  vcs_info # shows git branch
  zle reset-prompt
}
zle -N z_fd
bindkey '^d' z_fd

# Go to parent dir
function up_dir() {
  echo
  cd ../
  vcs_info # shows git branch
  zle reset-prompt
}
zle -N up_dir
bindkey '^b' up_dir

# Yank the current path 
function yank_path() {
    copy
    notify-send "yanked current directory path!"
}
zle -N yank_path
bindkey '^y' yank_path

# Edit file by the name or content
# Referred to
# - scripts/zfz.sh
# - https://www.reddit.com/r/zsh/comments/2uyo2a/launch_vims_ctrlp_from_zsh/
ctrlp() {
  local files
  IFS=$'\n' files=$(find ${1:-.} -path '*/\.git*' -prune \
                  -o -type f -print 2> /dev/null |fzf --query="$1" --multi --select-1 --exit-0 --prompt="File>")

  if [ -n "$files" ]; then
    < /dev/tty ${EDITOR:-vim} "${files[@]}"
  fi
}
grep_edit() {
  local file
  file="$(ag --nobreak --noheading -l ${BUFFER} | fzf --preview "ag ${BUFFER} {}" --prompt="Grep>" -0 -1 | awk -F: '{print $1}')"

  if [[ -n $file ]]
  then
     < /dev/tty $EDITOR $file
  fi
}
ctrlp_or_grep_edit() {
  if [ -z $BUFFER ]; then
    ctrlp
  else
    grep_edit
  fi
  zle reset-prompt
}
zle -N ctrlp_or_grep_edit
bindkey '^f' ctrlp_or_grep_edit

# Execute Cmd from History
function history_cmd() {
  # TODO: 一回enterおさないとコマンドが表示されていない
  fh;
  zle reset-prompt
  zle accept-line
}
zle -N history_cmd
bindkey '^h' history_cmd

# autojump
# NOTE: ctrl-jは制御文字でctrl-jのバインディングをすると、ctrl-returnでも自動でマッピングされるので、ctrl-gに変更した。 
z_faj() {
  faj;
  vcs_info # shows git branch
  zle accept-line
  zle reset-prompt
  clear
}
zle -N z_faj
bindkey '^g' z_faj

# fzf with ag
z_fg() {
  fg;
  vcs_info # shows git branch
  zle reset-prompt
}
zle -N z_fg
bindkey '^s' z_fg

# cmd-r is for ranger
# https://unix.stackexchange.com/questions/475310/how-to-bind-a-keyboard-shortcut-in-zsh-to-a-program-requiring-stdin
run_vifm () {
    echo
    vifm . < $TTY
    zle reset-prompt
}
zle -N run_vifm
bindkey '^r' run_vifm
